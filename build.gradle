plugins {
    id 'maven-publish'
    id 'fabric-loom' version "0.10-SNAPSHOT"
    // comment this out if you want to use the default decompiler or an older version of loom
    id "io.github.juuxel.loom-quiltflower-mini" version "1.1.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = 'legacy-fabric'
        url = 'https://maven.legacyfabric.net/'
    }
    // TODO Update Maven URL once new repository is made and group ID is updated
    maven {
        name = 'copetan'
        url = 'https://copetan.jfrog.io/artifactory/minecraft-maven/'
    }
}

// Comment when using loom 0.10
//minecraft {
//    intermediaryUrl = {
        // TODO Update Maven URL once new repository is made and group ID is updated
//        return "https://copetan.jfrog.io/artifactory/minecraft-maven/net/fabricmc/intermediary/" + it + "/intermediary-" + it + "-v2.jar";
//    }
//}

loom {
    // Uncomment when using loom 0.10
    // TODO Update Maven URL once new repository is made and group ID is updated
	setIntermediaryUrl('https://copetan.jfrog.io/artifactory/minecraft-maven/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar')


    runs {
        client {
            programArgs "--userProperties", "{}"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "me.copetan:feather:${project.feather_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation 'org.lwjgl.lwjgl:lwjgl_util:2.9.0'
    implementation 'org.lwjgl.lwjgl:lwjgl:2.9.0'
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.lwjgl.lwjgl:lwjgl_util:2.9.1-nightly-20131017') with module('org.lwjgl.lwjgl:lwjgl_util:2.9.0')
            substitute module('org.lwjgl.lwjgl:lwjgl:2.9.1-nightly-20131017') with module('org.lwjgl.lwjgl:lwjgl:2.9.0')
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) it.options.release = 8
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
